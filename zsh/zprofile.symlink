#!/bin/zsh
# Only attempt to source the configuration files
# if the dotfiles directory actually exists.
export DOTFILES="$HOME/.dotfiles";
if [ -d $DOTFILES ]; then
    # Retrieve all of the available *.zsh from every module.
    typeset -U config_files;
    config_files=($DOTFILES/*/*.zsh);

    # The paths for each module have to be loaded first since the rest of the
    # module might be dependant on it.
    for file in ${(M)config_files:#*/path.zsh}; do
        source $file;
    done;

    # Load everything except for the paths and completion since they're loaded separate.
    for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
        source $file;
    done;

    # Enable the auto-completion.
    autoload -U compinit
    compinit

    # The completion for each module have to be loaded last since the completion
    # might be dependant on paths, functions, etc. from the rest of the module.
    for file in ${(M)config_files:#*/completion.zsh}; do
        source $file;
    done;
    unset config_files file;
fi;

# Source the .extra file if it exists and is readable.
[ -f ~/.extra ] && [ -r ~/.extra ] && source ~/.extra;
